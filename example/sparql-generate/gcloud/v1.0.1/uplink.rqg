BASE <https://w3id.org/cocoon/data/v1.0.1/> 
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cocoon: <http://w3id.org/cocoon/v1.0.1#>
PREFIX schema: <https://schema.org/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX ssn: <http://www.w3.org/ns/ssn/>
PREFIX unit: <http://qudt.org/1.1/vocab/unit#>

GENERATE {
    # Uplink tests are only available for Compute services
    ?compute_iri a cocoon:ComputeService ;
        cocoon:hasProvider ?provider;
        ssn:hasProperty ?property_url;
        cocoon:inRegion <Region/{?provider_slug}/{?region}> ;
        cocoon:inZone ?zone .

    <Measurement/{?type_slug}/{?service_type_slug}/{?provider_slug}/150.203.213.249/lat=-35.271475/long=149.121434/{?completion_time}/{?region_zone_slug}> a cocoon:Measurement ;
        sosa:madeBySensor <Device/150.203.213.249/lat=-35.271475/long=149.121434> ;
        sosa:hasFeatureOfInterest  ?compute_iri ;
        sosa:hasResult [
            a schema:TypeAndQuantityNode;
            schema:amountOfThisGood ?value;
            schema:unitCode ?unit ] ;
        sosa:resultTime ?dateTime .
}
SOURCE <https://raw.githubusercontent.com/miranda-zhang/cloud-computing-schema/master/example/cloudharmony/selenium/gcloud/uplink.json> AS ?source
ITERATOR iter:JSONPath(?source,"$[*]") AS ?qos
WHERE {
    BIND( xsd:double( fun:JSONPath(?qos, '$.uplink.value') ) AS ?value)
    BIND( fun:JSONPath(?qos, '$.completion_time') AS ?completion_time)
    BIND( xsd:dateTime( ?completion_time ) AS ?dateTime)
    BIND( xsd:integer(fun:JSONPath(?qos, '$.size.lower.value')) AS ?min_size)
    BIND( fun:JSONPath(?qos, '$.region' ) AS ?region)
    BIND( fun:JSONPath(?qos, '$.subregion' ) AS ?subregion)  
    BIND( IF(BOUND(?subregion),STRAFTER(STRAFTER(?subregion,"-"),"-"), ?undefined) AS ?zone)
    BIND( IF(BOUND(?subregion),?subregion, ?region) AS ?region_zone_slug)
    # omitting the following as we can deduce those from ?min_size
    # BIND( xsd:string(fun:JSONPath(?qos, '$.size.upper.value')) AS ?max_size)
    # BIND( xsd:string(fun:JSONPath(?qos, '$.size.lower.unit')) AS ?lower_unit)
    # BIND( xsd:string(fun:JSONPath(?qos, '$.size.upper.unit')) AS ?upper_unit)
    BIND ( IF( ?min_size = 1 , <QuanlityOfService/UplinkSpeed-1-128-KB>, <QuanlityOfService/UplinkSpeed-256-10240-KB> ) AS ?property_url )
    BIND ( IF( ?min_size = 1 , "UplinkSpeed-1-128-KB", "UplinkSpeed-256-10240-KB" ) AS ?type_slug )
    BIND ( unit:MegabitsPerSecond AS ?unit ) # also known it is going to be Mb/s
    # having signle point of change for the following
    BIND ( "ComputeService" AS ?service_type_slug )
    BIND ( "Gcloud" as ?provider_slug )
    BIND ( cocoon:Gcloud as ?provider )
    BIND ( <{?type_slug}/{?service_type_slug}/{?provider_slug}/{?region_zone_slug}> as ?compute_iri ) 
}
