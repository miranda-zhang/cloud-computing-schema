BASE <https://w3id.org/cocoon/data/v1.0.1/> 
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cocoon: <http://w3id.org/cocoon/v1.0.1#>
PREFIX schema: <https://schema.org/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX ssn: <http://www.w3.org/ns/ssn/>
PREFIX unit: <http://qudt.org/vocab/unit#>

GENERATE {
    GENERATE {
        ?feature_iri a ?service_type_class ;
            cocoon:hasProvider ?provider;
            ssn:hasProperty ?property_url;
            cocoon:inRegion ?region_iri ;
            cocoon:inZone ?zone .

        <Measurement/{?type_slug}/{?service_type_slug}/{?provider_slug}/150.203.213.249/lat=-35.271475/long=149.121434/{?completion_time}/{?region_zone}> a cocoon:Measurement ;
            sosa:madeBySensor <Device/150.203.213.249/lat=-35.271475/long=149.121434> ;
            sosa:hasFeatureOfInterest  ?feature_iri ;
            sosa:hasResult [
                a schema:TypeAndQuantityNode;
                schema:amountOfThisGood ?average;
                schema:unitCode ?unit ] ;
            sosa:resultTime ?dateTime .
    }
    WHERE{
        BIND (
            COALESCE(
                IF(?type = "downlink", ?qos_downlink_iri , 1/0),
                IF(?type = "latency", <QuanlityOfService/Latency>, 1/0),
                <QuanlityOfService/DNSQueryLatency> # assume "dns"
            ) AS ?property_url
        )
        BIND (
            COALESCE(
                IF(?type = "downlink", ?downlinkMin_maxKB , 1/0),
                IF(?type = "latency", "Latency", 1/0),
                "DNSQueryLatency" # assume "dns"
            ) AS ?type_slug
        )
        BIND (
            COALESCE(
                IF(?service_type = "compute", "ComputeService", 1/0),
                IF(?service_type = "storage", "StorageService", 1/0),
                IF( ?service_type = "dns", "DNSService", 1/0),
                "CDNService" 
            ) AS ?service_type_slug
        )
        BIND (
            COALESCE(
                IF(?service_type = "compute", cocoon:ComputeService, 1/0),
                IF(?service_type = "storage", cocoon:StorageService, 1/0),
                IF( ?service_type = "dns", cocoon:DNSService, 1/0),
                cocoon:CDNService
            ) AS ?service_type_class
        )
        BIND (
            COALESCE(
                IF(?service_type = "compute", CONCAT(?region,"-",?zone), 1/0),
                IF(?service_type = "storage", ?region, 1/0),
                "" # dns or cdn
            ) AS ?region_zone
        )
        BIND ( 
            IF(
                ?service_type = "compute" || ?service_type = "storage",
                <Region/{?provider_slug}/{?region}>,
                ?undefined
            ) AS ?region_iri
        )
        BIND ( <{?type_slug}/{?service_type_slug}/{?provider_slug}/{?region_zone}> as ?feature_iri )
    } .
}
SOURCE <https://raw.githubusercontent.com/miranda-zhang/cloud-computing-schema/master/example/jq/gcloud/cloudharmony/downlink_latency.json> AS ?source
ITERATOR iter:JSONPath(?source,"$[*]") AS ?qos
WHERE {
    BIND( fun:JSONPath(?qos, '$.region' ) AS ?region)
    BIND( fun:JSONPath(?qos, '$.service_type' ) AS ?service_type)
    BIND( fun:JSONPath(?qos, '$.zone' ) AS ?zone)
    BIND( fun:JSONPath(?qos, '$.type' ) AS ?type)
    BIND( xsd:double( fun:JSONPath(?qos, '$.average') ) AS ?average)
    BIND( fun:JSONPath(?qos, '$.completion_time') AS ?completion_time)
    BIND( xsd:dateTime( ?completion_time ) AS ?dateTime)
    BIND( xsd:string(fun:JSONPath(?qos, '$.min_size')) AS ?min_size)
    BIND( xsd:string(fun:JSONPath(?qos, '$.max_size')) AS ?max_size)
    BIND ( IF( ?type = "downlink" , unit:MegabitsPerSecond , unit:MilliSecond ) AS ?unit)
    
    # Put dash between each word for easy parsing if needed
    BIND( CONCAT("DownlinkSpeed-",?min_size,"-",?max_size,"-KB") AS ?downlinkMin_maxKB )
    # <QuanlityOfService/DownlinkSpeed-{?min_size}-{?max_size}-KB> 
    BIND( IRI(CONCAT("QuanlityOfService/",?downlinkMin_maxKB)) AS ?qos_downlink_iri )

    # having signle point of change for the following
    BIND ( "Gcloud" as ?provider_slug )
    BIND ( cocoon:Gcloud as ?provider )
}
