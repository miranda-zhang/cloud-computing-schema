BASE <https://w3id.org/cocoon/data/v1.0.1/> 
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gr: <http://purl.org/goodrelations/v1#>
PREFIX cocoon: <https://raw.githubusercontent.com/miranda-zhang/cloud-computing-schema/master/ontology_dev/cocoon1.0.1.ttl>
PREFIX schema: <https://schema.org/>

GENERATE { 
  ?iri  a cocoon:NetworkStorage;
    rdfs:label ?name;
    cocoon:hasProvider ?provider;
    schema:dateModified ?date;
    GENERATE {
        ?iri  gr:hasPriceSpecification [ 
            a gr:CloudServicePriceSpecification ; 
                gr:hasCurrency "USD"^^xsd:string; 
                gr:hasCurrencyValue ?regionalPrice;
                gr:hasUnitOfMeasurement cocoon:GBPerMonth ;
                cocoon:hasRegion <{?release_date}/Region/{?provider_slug}/{?region}>;
        ]
    }
  	ITERATOR iter:JSONPath(?gcloudStorage,".prices[*]") AS ?prices
    WHERE {
        BIND (xsd:double(fun:JSONPath(?prices,".price")) AS ?regionalPrice)
        BIND (fun:JSONPath(?prices,".region") AS ?region)
    }   
  	.
 
}
SOURCE <https://raw.githubusercontent.com/miranda-zhang/cloud-computing-schema/master/example/jq/gcloud/v1.62/storage.json> AS ?source
ITERATOR iter:JSONPath(?source,"$[*]") AS ?gcloudStorage
WHERE {
    BIND (fun:JSONPath(?gcloudStorage,".name") AS ?name)
    # having signle point of change for the following
    BIND ( "Gcloud" as ?provider_slug )
    BIND ( cocoon:Gcloud as ?provider )
    BIND ( "2019-02-12" as ?release_date) # yr-month-day
    BIND ( <{?release_date}/NetworkStorage/{?provider_slug}/{?name}> as ?iri )
    BIND ( xsd:date(?release_date) as ?date )
}
