BASE <https://w3id.org/cocoon/data/v1.0.1/> 
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gr: <http://purl.org/goodrelations/v1#>
PREFIX unit: <http://qudt.org/vocab/unit#>
PREFIX cocoon: <https://raw.githubusercontent.com/miranda-zhang/cloud-computing-schema/master/ontology_dev/cocoon.v1.0.1.ttl#>
PREFIX schema: <https://schema.org/>

GENERATE { 
  ?iri a cocoon:ComputeService;
    rdfs:label ?vm_key;
    schema:dateModified ?date;
    cocoon:hasProvider ?provider;
    cocoon:numberOfCores ?cores;
    cocoon:hasMemory [
        a schema:TypeAndQuantityNode;
            schema:amountOfThisGood ?memory;
            schema:unitCode cocoon:GB
    ];
    cocoon:hasStorage [
        a schema:TypeAndQuantityNode;
            schema:amountOfThisGood ?diskSize;
            schema:unitCode cocoon:GB;
            schema:typeOfGood cocoon:LocalStorage;
    ];
    gr:hasPriceSpecification ?CloudStorageTransactionsPriceSpecification;

    GENERATE {
        ?iri gr:hasPriceSpecification [ 
            a cocoon:CloudServicePriceSpecification ; 
                gr:hasCurrency "USD"^^xsd:string; 
                gr:hasCurrencyValue ?value; 
                cocoon:inRegion <{?updated}/Region/{?provider_slug}/{?region}>;
                gr:hasUnitOfMeasurement unit:Hour ;
        ] 
    }
    ITERATOR iter:JSONListKeys(?prices) AS ?region
    WHERE {
        BIND (xsd:float ( fun:JSONPath(?prices,"$.{?region}.value") ) AS ?value)
    }   
  	.
 
}
SOURCE <https://raw.githubusercontent.com/miranda-zhang/cloud-computing-schema/master/example/jq/azure/2019-03-07/vm_base_offers.json> AS ?source
ITERATOR iter:JSONListKeys(?source) AS ?vm_key
WHERE {
    BIND (xsd:decimal ( fun:JSONPath(?source,"$.{?vm_key}.cores") ) AS ?cores)
    BIND (xsd:decimal ( fun:JSONPath(?source,"$.{?vm_key}.ram") ) AS ?memory)
    BIND (xsd:decimal ( fun:JSONPath(?source,"$.{?vm_key}.diskSize") ) AS ?diskSize)
    BIND (fun:JSONPath(?source,"$.{?vm_key}.prices") AS ?prices)
    # having signle point of change for the following
    BIND ( "Azure" as ?provider_slug )
    BIND ( cocoon:Azure as ?provider )
    BIND ( "2019-03-07" as ?updated) # yr-month-day
    BIND ( <{?updated}/ComputeService/{?provider_slug}/{?vm_key}> as ?iri )
    BIND ( xsd:date(?updated) as ?date )
    BIND ( <{?updated}/CloudStorageTransactionsPriceSpecification/{?provider_slug}/vm_base> as ?CloudStorageTransactionsPriceSpecification )
}
